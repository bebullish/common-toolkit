plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'maven-publish'
}

group 'cn.bebullish'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-json'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-json'
    testImplementation 'com.google.code.findbugs:annotations:3.0.1'
    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
}

java {
    withJavadocJar()
    withSourcesJar()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            url = codingArtifactsRepoUrl
            credentials {
                username = codingArtifactsGradleUsername
                password = codingArtifactsGradlePassword
            }
            name 'coding'
        }
        maven {
            url = bebullishArtifactsRepoUrl
            credentials {
                username = bebullishArtifactsGradleUsername
                password = bebullishArtifactsGradlePassword
            }
            name 'bebullish'
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = "$rootProject.name"
                description = 'common toolkit'
                url = 'https://github.com/bebullish/common-toolkit'
                developers {
                    developer {
                        id = 'bebullish'
                        name = 'bebullish'
                        email = '15048969966@163.com'
                    }
                }
            }
        }
    }
}